plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.gradleup.shadow") version "9.3.0"
    id 'maven-publish'
}

group = 'me.lyamray'

repositories {
    mavenCentral()
    maven { name = "papermc-repo"; url = uri("https://repo.papermc.io/repository/maven-public/") }
}

dependencies {
    // Paper
    compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")

    // HTTP + JSON
    implementation("com.squareup.okhttp3:okhttp:4.11.0")
    implementation("org.json:json:20230227")

    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.30")
    annotationProcessor("org.projectlombok:lombok:1.18.30")
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21

java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

shadowJar {
    archiveClassifier.set("")

    relocate("okhttp3", "me.lyamray.packify.libs.okhttp3")
    relocate("okio", "me.lyamray.packify.libs.okio")
    relocate("org.json", "me.lyamray.packify.libs.json")
}

tasks.named('jar') {
    enabled = false
}

tasks.named('assemble') {
    dependsOn(tasks.named('shadowJar'))
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            artifact(tasks.named("shadowJar"))
            artifact(tasks.named("sourcesJar"))
            artifact(tasks.named("javadocJar"))

            groupId = project.group
            artifactId = project.name
            version = project.version

            pom {
                name = project.name
                description = "API to upload, get & zip images into a texturepack and send to players."
                url = "https://github.com/LyamRay/Hexa-Packify"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }

                developers {
                    developer {
                        id = "lyamray"
                        name = "LyamRay"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/LyamRay/Hexa-Packify.git"
                    developerConnection = "scm:git:ssh://github.com/LyamRay/Hexa-Packify.git"
                    url = "https://github.com/LyamRay/Hexa-Packify"
                }
            }
        }
    }
}
