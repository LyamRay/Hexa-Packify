plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'me.lyamray'

repositories {
    mavenCentral()
    maven { name = "papermc-repo"; url = uri("https://repo.papermc.io/repository/maven-public/") }
}

def targetJavaVersion = 21

java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    // Paper API
    compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")

    // These are transitive dependencies for the plugin that uses this API
    api("com.squareup.okhttp3:okhttp:4.11.0")
    api("org.json:json:20230227")

    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.30")
    annotationProcessor("org.projectlombok:lombok:1.18.30")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = project.group
            artifactId = project.name
            version = project.version

            pom {
                name = project.name
                description = "API to upload, get & zip images into a texturepack and send to players."
                url = "https://github.com/LyamRay/Hexa-Packify"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }

                developers {
                    developer {
                        id = "lyamray"
                        name = "LyamRay"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/LyamRay/Hexa-Packify.git"
                    developerConnection = "scm:git:ssh://github.com/LyamRay/Hexa-Packify.git"
                    url = "https://github.com/LyamRay/Hexa-Packify"
                }
            }
        }
    }
}
